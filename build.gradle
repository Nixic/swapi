plugins {
    id "idea"
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'org.openapi.generator' version "6.0.1"
    id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

ext {
    set('swaggerAnnotationsVersion', "2.2.2")
    set('springFoxVersion', "2.3.0")
    set('fasterXmlJacksonAnnotationsVersion', "2.13.3")
    set('jacksonDatabindNullableVersion', "0.2.3")
    set('springdocOpenapiVersion', "1.6.11")
    set('lombokVersion', "1.18.24")
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
//    implementation 'org.springframework.boot:spring-boot-starter-security'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // https://mvnrepository.com/artifact/io.springfox/springfox-swagger-common
    implementation 'io.springfox:springfox-swagger-common:3.0.0'
    implementation "io.swagger.core.v3:swagger-annotations:${swaggerAnnotationsVersion}"
    implementation "io.springfox:springfox-swagger2:${springFoxVersion}"
    implementation "io.springfox:springfox-swagger-ui:${springFoxVersion}"
    implementation "org.springdoc:springdoc-openapi-ui:${springdocOpenapiVersion}"


    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-annotations
    implementation "com.fasterxml.jackson.core:jackson-annotations:${fasterXmlJacksonAnnotationsVersion}"
    implementation "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}"


    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}


task generateOpenApiSpec(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/resources/static/api.yaml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "com.example.swapi"
    invokerPackage = "com.example.utils"
    modelPackage = "com.example.model"
    typeMappings = [
            'bigdecimal': 'java.math.BigDecimal'
    ]
    configOptions = [
            dateLibrary    : "java8",
            delegatePattern: "true"
    ]

    additionalProperties = [
            "skipDefaultInterface": "true",
            "interfaceOnly"       : "true",
            "booleanGetterPrefix" : "is",
            "useTags"             : "true"
    ]
    importMappings = [
            'Pageable': 'org.springframework.data.domain.Pageable',
            'PageInfo': 'com.example.utils.PageInfo'
    ]
}
compileJava.dependsOn(generateOpenApiSpec)

def generatedSourcesDir = file("build/generated/src/main/java")
sourceSets {
    main {
        java {
            srcDir 'build/generated/src/main/java'
        }
    }
}
idea {
    module {
        sourceDirs += generatedSourcesDir
        generatedSourceDirs += generatedSourcesDir
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
